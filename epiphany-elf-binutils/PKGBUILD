# Maintainer: Jonathan Scott Tinkham <sctincman@gmail.com>
# Adapted from arm-none-eabi-binutils by Anatol Pomozov <anatol.pomozov@gmail.com>

_target=epiphany-elf
pkgname=$_target-binutils
pkgver=2.30
pkgrel=1
pkgdesc='A set of programs to assemble and manipulate binary and object files for the Adapteva Epiphany target'
arch=(armv7h armv7l)
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(zlib)
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.bz2)
sha256sums=('efeade848067e9a03f1918b1da0d37aaffa0b0127a06b5e9236229851d9d0c09')

build() {
  cd binutils-$pkgver

  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  ./configure --target=$_target \
              --prefix=/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH \
              --enable-multilib \
              --enable-interwork \
              --with-gnu-as \
              --with-gnu-ld \
              --disable-nls \
	      --disable-werror

  make configure-host
  make
}

check() {
  cd binutils-$pkgver
  
  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check
}

package() {
  cd binutils-$pkgver

  make DESTDIR="$pkgdir" install

  ln -s epiphany-elf-addr2line $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-addr2line
  ln -s epiphany-elf-ar        $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-ar
  ln -s epiphany-elf-as        $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-as
  ln -s epiphany-elf-c++filt   $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-c++filt
  ln -s epiphany-elf-elfedit   $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-elfedit
  ln -s epiphany-elf-ld        $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-ld
  ln -s epiphany-elf-ld.bfd    $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-ld.bfd
  ln -s epiphany-elf-nm        $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-nm
  ln -s epiphany-elf-objcopy   $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-objcopy
  ln -s epiphany-elf-objdump   $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-objdump
  ln -s epiphany-elf-ranlib    $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-ranlib
  ln -s epiphany-elf-readelf   $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-readelf
  ln -s epiphany-elf-size      $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-size
  ln -s epiphany-elf-strings   $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-strings
  ln -s epiphany-elf-strip     $pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/bin/e-strip


  # Remove file conflicting with host binutils and manpages for MS Windows tools
  rm "$pkgdir"/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/share/man/man1/${_target}-{dlltool,nlmconv,windres,windmc}*

  # Remove info documents that conflict with host version
  rm -rf "$pkgdir"/opt/adapteva/esdk.2016.11/tools/e-gnu.$CARCH/share/info
}
