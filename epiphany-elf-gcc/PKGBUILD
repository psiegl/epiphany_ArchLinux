# Maintainer: Jonathan Scott Tinkham <sctincman@gmail.com>
# Adapted from arm-none-eabi- by Anatol Pomozov

_target=epiphany-elf
pkgname=$_target-gcc
pkgver=4.9
pkgrel=1
_snapshot=4.9-20160727
_newlibver=2.2.0-1
pkgdesc='The GNU Compiler Collection - cross compiler for the Adapteva Epiphany target'
arch=(i686 x86_64 armv7h)
url='http://gcc.gnu.org/'
license=(GPL LGPL FDL)
depends=($_target-binutils libmpc)
makedepends=(gmp mpfr)
#checkdepends=(dejagnu)
options=(!emptydirs !strip staticlibs)
source=(#ftp://gcc.gnu.org/pub/gcc/releases/gcc-$pkgver/gcc-$pkgver.tar.bz2
        ftp://gcc.gnu.org/pub/gcc/snapshots/$_snapshot/gcc-$_snapshot.tar.bz2
        ftp://sourceware.org/pub/newlib/newlib-$_newlibver.tar.gz)
sha256sums=('8fbb0ba3d9ab0400e6acc9cf610e525d6ce0501b3c32dfb36dda01875e1b6280'
            'c1d53fd1765d955ac513aa6e2f576455ee7507072d400314495c8a8a26ebc8ec')

if [ -n "$_snapshot" ]; then
  _basedir=gcc-$_snapshot
else
  _basedir=gcc-$pkgver
fi

prepare() {
  cd "$srcdir/$_basedir"
  echo $pkgver > gcc/BASE-VER

  # Move newlib and libgloss to gcc source directory
  mv "$srcdir"/newlib-$_newlibver/newlib "$srcdir/$_basedir"
  mv "$srcdir"/newlib-$_newlibver/libgloss "$srcdir/$_basedir"

  # hack! - some configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" "$srcdir"/$_basedir/{libiberty,gcc}/configure
}

build() {
  cd $_basedir
  mkdir -p build
  cd build

  export CFLAGS_FOR_TARGET='-O2 -g -pipe'
  export CXXFLAGS_FOR_TARGET='-O2 -g -pipe'

  ../configure --target=$_target \
               --prefix=/opt/adapteva/esdk.2016.11/tools/e-gnu.armv7l \
               --libexecdir=/opt/adapteva/esdk.2016.11/tools/e-gnu.armv7/lib \
               --with-pkgversion='Arch Repository' \
               --with-bugurl='https://bugs.archlinux.org/' \
               --enable-multilib \
               --enable-interwork \
               --enable-languages=c,c++ \
               --with-newlib \
               --with-gnu-as \
               --with-gnu-ld \
               --disable-nls \
               --with-headers=newlib/libc/include \
               --disable-werror

  make
}

check() {
  cd $_basedir/build
}

package() {
  cd $_basedir/build

  make DESTDIR="$pkgdir" install -j1

  # Strip host objects but do not touch target one (strip corrupts access.o in libepiphany.a)
  find "$pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.armv7l/lib" -executable -type f -exec /usr/bin/strip --strip-debug '{}' \;
  find "$pkgdir/opt/adapteva/esdk.2016.11/tools/e-gnu.armv7l/bin" -executable -type f -exec /usr/bin/strip --strip-debug '{}' \;

  # Remove files that conflict with host gcc package
  rm -r "$pkgdir"/opt/adapteva/esdk.2016.11/tools/e-gnu.armv7l/share/man/man7
  rm -r "$pkgdir"/opt/adapteva/esdk.2016.11/tools/e-gnu.armv7l/share/info
  rm -r "$pkgdir"/opt/adapteva/esdk.2016.11/tools/e-gnu.armv7l/share/gcc-$pkgver
}

